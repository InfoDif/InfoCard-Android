<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns='http://www.w3.org/1999/xhtml'>

<head>
<title>Dump Content of Log Window to File</title>
<meta name="generator" content="Halibut version 1.0 (NSIS Custom Build) xhtml-backend" />
<link rel="stylesheet" href="style.css" type='text/css' /><script language="JavaScript" type="text/javascript" src="chmlink.js"></script>
</head>

<body>
<p><a href='SectionD.3.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='SectionD.5.html'>Next</a></p>
<a name="D.4"></a><h2>D.4 Dump Content of Log Window to File</h2>

<p>This function will dump the log of the installer (installer details) to a file of your choice. I created this function for Afrow_UK who requested a way to dump the log to a file in <a href="http://forums.winamp.com/showthread.php?s=&threadid=125431">this forum thread</a>.</p>

<p>To use it, push a file name and call it. It will dump the log to the file specified. For example:</p>
<pre>GetTempFileName $0
Push $0
Call DumpLog
</pre>

<p>Here is the function:</p>
<pre>!define LVM_GETITEMCOUNT 0x1004
!define LVM_GETITEMTEXT 0x102D

Function DumpLog
  Exch $5
  Push $0
  Push $1
  Push $2
  Push $3
  Push $4
  Push $6

  FindWindow $0 &quot;#32770&quot; &quot;&quot; $HWNDPARENT
  GetDlgItem $0 $0 1016
  StrCmp $0 0 error
  FileOpen $5 $5 &quot;w&quot;
  StrCmp $5 0 error
    SendMessage $0 ${LVM_GETITEMCOUNT} 0 0 $6
    System::Alloc ${NSIS_MAX_STRLEN}
    Pop $3
    StrCpy $2 0
    System::Call &quot;*(i, i, i, i, i, i, i, i, i) i \
      (0, 0, 0, 0, 0, r3, ${NSIS_MAX_STRLEN}) .r1&quot;
    loop: StrCmp $2 $6 done
      System::Call &quot;User32::SendMessageA(i, i, i, i) i \
        ($0, ${LVM_GETITEMTEXT}, $2, r1)&quot;
      System::Call &quot;*$3(&amp;t${NSIS_MAX_STRLEN} .r4)&quot;
      FileWrite $5 &quot;$4$\r$\n&quot;
      IntOp $2 $2 + 1
      Goto loop
    done:
      FileClose $5
      System::Free $1
      System::Free $3
      Goto exit
  error:
    MessageBox MB_OK error
  exit:
    Pop $6
    Pop $4
    Pop $3
    Pop $2
    Pop $1
    Pop $0
    Exch $5
FunctionEnd
</pre>

<p>written by KiCHiK</p>
<p><a href='SectionD.3.html'>Previous</a> | <a href='Contents.html'>Contents</a> | <a href='SectionD.5.html'>Next</a></p>

<hr />

<address>
</address>
</body>

</html>
